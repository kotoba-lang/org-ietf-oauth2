(ns oauth2.core-test
  (:require [clojure.test :refer [deftest testing is]]
            [clojure.string :as str]
            [oauth2.core :as oauth2]))

(deftest authorization-url-test
  (testing "builds a well-formed authorization request URL"
    (let [url (oauth2/authorization-url
               {:authorize-endpoint "https://as.example.com/authorize"
                :client-id "abc123"
                :redirect-uri "https://app.example.com/cb"
                :scope "openid profile"
                :state "xyz"})]
      (is (str/starts-with? url "https://as.example.com/authorize?"))
      (is (str/includes? url "response_type=code"))
      (is (str/includes? url "client_id=abc123"))
      (is (str/includes? url "redirect_uri=https%3A%2F%2Fapp.example.com%2Fcb"))
      (is (str/includes? url "scope=openid%20profile"))
      (is (str/includes? url "state=xyz"))))
  (testing "includes PKCE params when present"
    (let [url (oauth2/authorization-url
               {:authorize-endpoint "https://as.example.com/authorize"
                :client-id "abc123"
                :redirect-uri "https://app.example.com/cb"
                :code-challenge "E9Melhoa2OwvFrEMTJguCHaoeK1t8URWbuGJSstw-cM"
                :code-challenge-method "S256"})]
      (is (str/includes? url "code_challenge=E9Melhoa2OwvFrEMTJguCHaoeK1t8URWbuGJSstw-cM"))
      (is (str/includes? url "code_challenge_method=S256")))))

(deftest parse-authorization-response-test
  (testing "success"
    (is (= {:code "SplxlOBeZQQYbYS6WxSbIA" :state "xyz"}
           (oauth2/parse-authorization-response
            {:code "SplxlOBeZQQYbYS6WxSbIA" :state "xyz"}))))
  (testing "error, RFC 6749 §4.1.2.1"
    (is (= {:error "access_denied" :error-description "user said no"}
           (oauth2/parse-authorization-response
            {:error "access_denied" :error_description "user said no"})))))

(deftest token-request-body-test
  (testing "authorization_code grant"
    (is (= {:grant_type "authorization_code"
            :code "SplxlOBeZQQYbYS6WxSbIA"
            :redirect_uri "https://app.example.com/cb"
            :client_id "abc123"
            :client_secret "s3cr3t"
            :code_verifier "verifier-value"}
           (oauth2/token-request-body
            {:grant-type "authorization_code"
             :code "SplxlOBeZQQYbYS6WxSbIA"
             :redirect-uri "https://app.example.com/cb"
             :client-id "abc123"
             :client-secret "s3cr3t"
             :code-verifier "verifier-value"}))))
  (testing "refresh_token grant"
    (is (= {:grant_type "refresh_token"
            :refresh_token "r3fr3sh"
            :client_id "abc123"
            :client_secret "s3cr3t"
            :scope nil}
           (oauth2/token-request-body
            {:grant-type "refresh_token"
             :refresh-token "r3fr3sh"
             :client-id "abc123"
             :client-secret "s3cr3t"}))))
  (testing "client_credentials grant"
    (is (= {:grant_type "client_credentials"
            :client_id "abc123"
            :client_secret "s3cr3t"
            :scope "read"}
           (oauth2/token-request-body
            {:grant-type "client_credentials"
             :client-id "abc123"
             :client-secret "s3cr3t"
             :scope "read"}))))
  (testing "unknown grant type throws"
    (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs cljs.core.ExceptionInfo)
                 (oauth2/token-request-body {:grant-type "password"})))))

(deftest parse-token-response-test
  (testing "success"
    (is (= {:access-token "2YotnFZFEjr1zCsicMWpAA"
            :token-type "bearer"
            :expires-in 3600
            :refresh-token "tGzv3JOkF0XG5Qx2TlKWIA"
            :scope "read write"}
           (oauth2/parse-token-response
            {:access_token "2YotnFZFEjr1zCsicMWpAA"
             :token_type "bearer"
             :expires_in 3600
             :refresh_token "tGzv3JOkF0XG5Qx2TlKWIA"
             :scope "read write"}))))
  (testing "error response, RFC 6749 §5.2"
    (let [ex (try (oauth2/parse-token-response {:error "invalid_grant"
                                                 :error_description "expired code"})
                   (catch #?(:clj Exception :cljs :default) e e))]
      (is (= "invalid_grant" (:error (ex-data ex))))
      (is (= "expired code" (:error-description (ex-data ex)))))))
